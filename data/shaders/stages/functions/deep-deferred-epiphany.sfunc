//----- Unpacking

float gBufferUnpackFloat(GBufferNode node, inout uint startIndex) {
    float value = uintBitsToFloat(node.materialData[startIndex++]);
    return value;
}

vec3 gBufferUnpackVec3(GBufferNode node, inout uint startIndex) {
    vec3 value;
    value.x = uintBitsToFloat(node.materialData[startIndex++]);
    value.y = uintBitsToFloat(node.materialData[startIndex++]);
    value.z = uintBitsToFloat(node.materialData[startIndex++]);
    return value;
}

// Stored using spherical coordinates
vec3 gBufferUnpackVec3Normalized(GBufferNode node, inout uint startIndex) {
    float phi = uintBitsToFloat(node.materialData[startIndex++]);
    float theta = uintBitsToFloat(node.materialData[startIndex++]);
    
    vec3 value;
    value.x = sin(phi) * cos(theta);
    value.y = sin(phi) * sin(theta);
    value.z = cos(phi);
    return value;
}

vec4 gBufferUnpackVec4(GBufferNode node, inout uint startIndex) {
    vec4 value;
    value.x = uintBitsToFloat(node.materialData[startIndex++]);
    value.y = uintBitsToFloat(node.materialData[startIndex++]);
    value.z = uintBitsToFloat(node.materialData[startIndex++]);
    value.w = uintBitsToFloat(node.materialData[startIndex++]);
    return value;
}

//----- Lights

bool epiphanyLight(vec3 wPosition, out vec3 lightDirection, out float lightIntensity) {
    lightIntensity = 0;

    switch (light.type) {
        // Point light
        case 0: {
            lightDirection = light.wPosition.xyz - wPosition.xyz;
            float lightDistance = length(lightDirection);

            float radius = uintBitsToFloat(light.data[0].x);
            if (lightDistance < radius) {
                lightIntensity = 1 - (lightDistance * lightDistance) / (radius * radius);
            }
            break;
        }

        // Directional light
        case 1: {
            lightDirection.x = uintBitsToFloat(light.data[0].x);
            lightDirection.y = uintBitsToFloat(light.data[0].y);
            lightDirection.z = uintBitsToFloat(light.data[0].z);

            // @todo Have that settable
            lightIntensity = 1;

            break;
        }

        default: {
            return false;
        }
    }

    lightDirection = normalize(lightDirection);
    return true;
}
