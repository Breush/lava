#softdefine USE_CAMERA_PUSH_CONSTANT
#softdefine USE_FLAT_PUSH_CONSTANT
#softdefine USE_SHADOW_MAP_PUSH_CONSTANT

#if USE_CAMERA_PUSH_CONSTANT || USE_FLAT_PUSH_CONSTANT || USE_SHADOW_MAP_PUSH_CONSTANT
layout(std430, push_constant) uniform PushConstantUbo {
    // Camera UBO
    #if USE_CAMERA_PUSH_CONSTANT
    vec4 cameraViewTransform0;
    vec4 cameraViewTransform1;
    vec4 cameraViewTransform2;
    vec4 cameraProjectionFactors0;
    vec4 cameraProjectionFactors1;
    #endif

    // Flat UBO
    #if USE_FLAT_PUSH_CONSTANT
    vec4 flatTransform;   // 00 01 10 11
    vec4 flatTranslation; // Only XY
    #endif

    // Shadow map UBO
    #if USE_SHADOW_MAP_PUSH_CONSTANT
    mat4 cascadeTransform;
    #endif
} pushConstants;
#endif

//----- Camera

#if USE_CAMERA_PUSH_CONSTANT
struct CameraUbo {
    mat4 viewTransform;
    mat4 projectionMatrix;
    mat4 viewTransformInverse;
    mat4 projectionMatrixInverse;
    vec4 position;
    uvec2 extent;
} camera;

void setupCamera() {
    camera.viewTransform = transpose(mat4(pushConstants.cameraViewTransform0,
                                          pushConstants.cameraViewTransform1,
                                          pushConstants.cameraViewTransform2,
                                          vec4(0, 0, 0, 1)));

    camera.projectionMatrix = mat4(0);
    camera.projectionMatrix[0][0] = pushConstants.cameraProjectionFactors0[0];
    camera.projectionMatrix[1][1] = pushConstants.cameraProjectionFactors0[1];
    camera.projectionMatrix[2][2] = pushConstants.cameraProjectionFactors0[2];
    camera.projectionMatrix[3][2] = pushConstants.cameraProjectionFactors0[3];
    camera.projectionMatrix[2][0] = pushConstants.cameraProjectionFactors1[0];
    camera.projectionMatrix[2][1] = pushConstants.cameraProjectionFactors1[1];
    camera.projectionMatrix[2][3] = -1;

    camera.viewTransformInverse = inverse(camera.viewTransform);
    camera.projectionMatrixInverse = inverse(camera.projectionMatrix);

    camera.position = camera.viewTransformInverse[3];
    camera.extent = uvec2(pushConstants.cameraProjectionFactors1[2], pushConstants.cameraProjectionFactors1[3]);
}

#include "../helpers/camera.sfunc"
#endif

//----- Flat

#if USE_FLAT_PUSH_CONSTANT
struct FlatUbo {
    mat3 transform;
} flatUbo; // @todo Sorry for extra "Ubo", but flat is a keyword in GLSL...

void setupFlat() {
    flatUbo.transform = mat3(pushConstants.flatTransform.xy, 0,
                             pushConstants.flatTransform.zw, 0,
                             pushConstants.flatTranslation.xy, 1);
}
#endif
