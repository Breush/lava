/**
 * Roughness-metallic material.
 * Based on https://github.com/KhronosGroup/glTF-WebGL-PBR
 *
 * It is using a Cook-Torrance Model to compute lighting.
 */

// The only thing that is shared between geometry and epiphany.
struct {
    float(8) opacity;   // This (8) means that the value can be stored on 8 bits,
    vec3(8) albedo;     // as we want to reduce the size of that structure at most.
    nvec3(16) normal;   // It is normalized, storing costs one less.
    float(8) occlusion;
    float(8) roughness;
    float(8) metallic;
} gBuffer;

// Everything that the user can control,
// with their default values.
uniform {
    float ghostFactor = 0;
    vec4 albedoColor = vec4(1, 1, 1, 1);
    float roughnessFactor = 1;
    float metallicFactor = 1;
    texture2d normalMap = "normal";
    texture2d albedoMap = "white";
    texture2d occlusionMap = "white";           // Red channel.
};

// ----- GEOMETRY

geometry {
    // In the geometry main, our role is to fill the G-Buffer
    // with the useful data for lighting computation.
    bool main() {
        vec4 albedo = texture(albedoMap, uv);
        if (albedo.a == 0) discard;
        albedo = srgbToLinearFast(albedo) * albedoColor;

        vec3 normal = 2 * texture(normalMap, uv).rgb - 1;
        float occlusion = texture(occlusionMap, uv).r;

        vec3 position = positionFromDepth(fragmentDepth, gl_FragCoord.xy);
        vec3 v = normalize(camera.position.xyz - position);

        // @note VR-puzzle specific
        float ghostPower = dot(normalize(tbn * normal), v);
        float ghostColor = max(0.5, abs(cos(ghostPower * 1000)));
        albedo.rgb += ghostFactor * 0.9;
        // albedo.rgb = vec3(ghostPower);

        gBuffer.opacity = albedo.a;
        gBuffer.albedo = albedo.rgb;
        gBuffer.normal = normalize(tbn * normal);
        gBuffer.occlusion = occlusion;
        gBuffer.roughness = clamp(roughnessFactor, 0.04, 1);
        gBuffer.metallic = clamp(metallicFactor, 0, 1);

        // Returns whether the fragment is considered translucent.
        return albedo.a < 1.0;
    }
}

// ----- EPIPHANY

epiphany {
    struct RmPbrData {
        float roughness;        // Material roughness.
        vec3 n;                 // Normal vector.
        vec3 v;                 // View vector. Targeting the camera.
        float n_l;              // Angle (cosine) between normal and light vector.
        float n_v;              // Angle (cosine) between normal and view vector.
        float n_h;              // Angle (cosine) between normal and half vector.
        float v_h;              // Angle (cosine) between view and half vector.
        vec3 reflectance0;      // Full reflectance color (normal incidence angle).
        vec3 reflectance90;     // Reflectance color at grazing angle.
        vec3 diffuseColor;      // Color factor for diffuse lighting.
        vec3 specularColor;     // Color factor for specular lighting.
    };

    /**
     * Basic Lambertian diffuse.
     * From https://archive.org/details/lambertsphotome00lambgoog.
     */
    vec3 diffuse(vec3 diffuseColor)
    {
        return diffuseColor / PI;
    }

    /**
     * The Fresnel reflectance term.
     * From https://www.cs.virginia.edu/~jdl/bib/appearance/analytic%20models/schlick94b.pdf (Eq. 15).
     */
    vec3 specularReflection(RmPbrData pbr)
    {
        return pbr.reflectance0 + (pbr.reflectance90 - pbr.reflectance0) * pow(1.0 - pbr.v_h, 5.0);
    }

    /**
     * Calculation of the lighting contribution from the environment map.
     * The environment map has to be pre-processed.
     */
    vec3 environmentContribution(RmPbrData pbr)
    {
        // Early out when there is no environment map.
        vec4 diffuseSample = texture(environmentIrradianceMap, fixCubeUvw(pbr.n));
        if (diffuseSample.a == 0) return vec3(0);

        float lod = (pbr.roughness * (ENVIRONMENT_RADIANCE_MIP_LEVELS_COUNT - 1));

        vec3 r = normalize(reflect(-pbr.v, pbr.n));

        vec2 brdfSamplePoint = clamp(vec2(pbr.n_v, pbr.roughness), vec2(0), vec2(1));
        vec2 brdf = texture(brdfLut, brdfSamplePoint).rg;

        vec4 specularSample = textureLod(environmentRadianceMap, fixCubeUvw(r), lod);

        vec3 diffuseLight = srgbToLinearFast(diffuseSample).rgb;
        vec3 specularLight = srgbToLinearFast(specularSample).rgb;

        vec3 diffuse = diffuseLight * pbr.diffuseColor;
        vec3 specular = specularLight * (pbr.specularColor * brdf.x + brdf.y);

        return diffuse + specular;
    }

    /**
     * Smith Joint GGX
     * Note: Vis = G / (4 * NdotL * NdotV)
     *
     * The specular geometric attenuation,
     * where rougher material will reflect less light back to the viewer.
     * From http://blog.selfshadow.com/publications/s2013-shading-course/karis/s2013_pbs_epic_notes_v2.pdf
     * with alphaRoughness from http://blog.selfshadow.com/publications/s2012-shading-course/burley/s2012_pbs_disney_brdf_notes_v3.pdf.
     * See Eric Heitz. 2014. Understanding the Masking-Shadowing Function in Microfacet-Based BRDFs. Journal of Computer Graphics Techniques.
     */
    float visibilityOcclusion(RmPbrData pbr)
    {
        float n_l = pbr.n_l;
        float n_v = pbr.n_v;
        float r2 = pbr.roughness * pbr.roughness;

        float GGXV = n_l * sqrt(n_v * n_v * (1.0 - r2) + r2);
        float GGXL = n_v * sqrt(n_l * n_l * (1.0 - r2) + r2);

        float GGX = GGXV + GGXL;
        if (GGX > 0.0)
        {
            return 0.5 / GGX;
        }
        return 0.0;
    }

    /**
     * The distribution of microfacet normals across the area being drawn.
     * From "Average Irregularity Representation of a Roughened Surface for Ray Reflection" by T. S. Trowbridge, and K. P. Reitz
     * and the distribution from http://blog.selfshadow.com/publications/s2013-shading-course/karis/s2013_pbs_epic_notes_v2.pdf (Eq. 3).
     */
    float microfacetDistribution(RmPbrData pbr)
    {
        float r2 = pbr.roughness * pbr.roughness;
        float f = (r2 - 1.0) * pbr.n_h * pbr.n_h + 1.0;
        return r2 / (PI * f * f);
    }

    /**
     * Contribution for a single light.
     */
    vec3 lightContribution(vec3 position, RmPbrData pbr, inout float shadow)
    {
        vec3 l;
        float lightIntensity;
        if (!epiphanyLight(position, l, lightIntensity)) {
            return vec3(0);
        }

        shadow = max(shadow, epiphanyShadow(l, position, pbr.n));

        // ----- Pre-computing

        vec3 h = normalize(l + pbr.v);
        pbr.n_l = clamp(dot(pbr.n, l), 0.001, 1.0);
        pbr.n_h = clamp(dot(pbr.n, h), 0.0, 1.0);
        pbr.v_h = clamp(dot(pbr.v, h), 0.0, 1.0);

        // @todo Have light color uniform
        vec3 lightColor = vec3(1);
        float lightEnergy = lightIntensity * pbr.n_l;

        // ----- BRDF

        // Calculate the shading terms for the microfacet specular shading model
        vec3 F = specularReflection(pbr);
        float Vis = visibilityOcclusion(pbr);
        float D = microfacetDistribution(pbr);

        // Reflectance (BRDF) scaled by the energy of the light.
        vec3 diffuseContrib = (1.0 - F) * diffuse(pbr.diffuseColor);
        vec3 specularContrib = F * Vis * D;

        return lightEnergy * lightColor * (diffuseContrib + specularContrib);
    }

    vec4 main() {
        // ----- Common variables

        vec3 position = positionFromDepth(fragmentDepth, fragmentPosition);

        RmPbrData pbr;
        pbr.n = gBuffer.normal;
        pbr.v = normalize(camera.position.xyz - position);
        pbr.n_v = clamp(abs(dot(pbr.n, pbr.v)), 0.001, 1.0);

        // @note Convert to material roughness by squaring the perceptual roughness.
        pbr.roughness = gBuffer.roughness * gBuffer.roughness;

        // ----- Diffuse

        const vec3 f0 = vec3(0.04);
        pbr.diffuseColor = gBuffer.albedo * (1.0 - f0);
        pbr.diffuseColor *= (1.0 - gBuffer.metallic);

        // ----- Specular

        pbr.specularColor = mix(f0, gBuffer.albedo, gBuffer.metallic);

        // ----- Reflectance

        // For typical incident reflectance range (between 2% to 100%),
        // set the grazing reflectance to 100% for typical fresnel effect.
        // For very low reflectance range on highly diffuse objects (below 2%),
        // incrementally reduce grazing reflecance to 0%.
        float reflectance = max(max(pbr.specularColor.r, pbr.specularColor.g), pbr.specularColor.b);
        float reflectance90 = clamp(reflectance * 50.0, 0.0, 1.0);
        pbr.reflectance0 = pbr.specularColor;
        pbr.reflectance90 = vec3(reflectance90);

        // ----- Lights contribution

        vec3 color = vec3(0);

        // @todo Iterate over all lights
        float shadow = 0;
        vec3 lightContributionColor = lightContribution(position, pbr, shadow);
        if (lightContributionColor.x >= 0) {
            color += lightContributionColor;
        }

        // ----- Environment contribution

        color += environmentContribution(pbr);

        // ----- Occlusion

        color *= gBuffer.occlusion;

        return vec4(linearToSrgb(color), gBuffer.opacity);
    }
}
